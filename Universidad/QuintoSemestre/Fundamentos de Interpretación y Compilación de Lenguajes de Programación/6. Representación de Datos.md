La representación de datos se refiere a la forma en que los datos son organizados y presentados para su almacenamiento, manipulación, transmisión o visualización por parte de un sistema informático o una persona. La representación de datos es esencial para que los sistemas informáticos puedan entender y procesar la información de manera eficiente.

# Abstracción de datos
La **abstracción de datos** es un concepto fundamental en informática y programación, y se refiere a la idea de separar la representación y el comportamiento de los datos de su implementación concreta. En otras palabras, se trata de simplificar la complejidad al mostrar solo los detalles relevantes y ocultar los detalles innecesarios.

Un ejemplo común de abstracción de datos en programación es el uso de clases y objetos en la programación orientada a objetos. Una clase define la estructura y el comportamiento de un tipo de datos, mientras que los objetos son instancias específicas de esa clase. Los programadores pueden interactuar con los objetos a través de métodos y propiedades públicas, sin necesidad de conocer los detalles internos de cómo se implementan esos métodos y propiedades.

Al definir un conjunto de valores, se está definiendo un nuevo tipo de dato. En estos tipos de datos se definen los valores, representaciones y operaciones que pueden realizarse sobre el mismo. La representación de los datos es bastante compleja y, por ello, para cambiarla se lleva a cabo una tarea conocida como la abstracción de datos.

La abstracción de datos divide los tipos de datos en *interfaz* e *implementación*: la interfaz da información sobre qué representa el tipo de dato, sus operaciones y propiedades; mientras que la implementación proporciona una representación específica y un código para las operaciones que utilizan esa representación. La parte más importante de la implementación es la especificación de cómo los datos son representados.

**Ejemplo:** Una posible representación para los enteros no negativos es la *Bignum*, en la que los números son representados en base $N$, para algún entero grande $N$. Dicha representación es una lista que consiste de números entre $0$ y $N-1$.

$$\lceil n \rceil = \cases{() \quad n=0\\ (cons\text{ }r\text{ }\lceil q \rceil \quad n=qN+r, 0\leq r < N)}$$

Por lo tanto, si $N=16$, entonces:
- $\lceil 33 \rceil = (1 \text{ }2) \quad ((1\times16^0)+(2\times16^1))$
- $\lceil 258 \rceil = (2 \text{ }0\text{ }1) \quad ((2\times16^0)+(0\times16^1)+(1\times16^2))$

# Estrategias para representar tipos de datos
Hay varias estrategias que se pueden utilizar para representar diferentes tipos de datos. La elección de la estrategia adecuada depende del tipo de datos, el contexto de la aplicación y los requisitos de rendimiento. Es importante recalcar que, cuando se usa la abstracción de datos, los programas tienen independencia de representación.

- Se ilustran las estrategias usando el tipo de dato *ambiente*.
- Un ambiente asocia un valor con cada elemento de un conjunto finito de variables.
- Un ambiente puede ser usado para asocial las variables con sus valores en la implementación de un lenguaje de programación.
- Las variables pueden ser representadas de cualquier manera, siempre y cuando sea posible verificar la igualdad entre dos variables.
- Las variables se pueden representar mediante símbolos, cadenas, referencias en una tabla Hash o incluso números.
- Un ambiente es una función cuyo dominio es un conjunto finito de variables y cuyo rango es el conjunto de todos los valores del lenguaje de programación.
- De acuerdo a la notación matemática, los ambientes representan todos los conjuntos de la forma $\{(s_1,v_1),...,(s_n,v_n)\}$, donde $s_i$ corresponden a símbolos diferentes entre sí y $v_i$ a valores del lenguaje de programación.

## Interfaz
La interfaz del tipo de dato ambiente cuenta con tres procedimientos:

- $(empty-env) \quad =\lceil \emptyset \rceil$ (crea un ambiente vacío)
- $(apply-env \text{ } \lceil f \rceil \text{ } s) \quad =f(s)$ (busca un valor en un ambiente)
- $(extend-env \text{ }var\text{ }v\text{ }\lceil f \rceil) \quad =\lceil g \rceil$ (extiende un ambiente

donde $g(s') = \cases{v \quad \text{si }s'=var\\f(s') \quad \text{de otra forma}}$


De esta manera:

- El procedimiento `empty-env` debe producir una representación del ambiente vacío.
- El procedimiento `apply-env` aplica una representación de un ambiente a un argumento.
- El procedimiento `(extend-env var val env)` produce un nuevo ambiente que se comporta como `env`, excepto que su valor en el símbolo `var` es val. 

**Ejemplo de definición de un entorno:**
```racket
(define e
  (extend-env 'd 6
              (extend-env 'y 8
                          (extend-env 'x 7
                                      (extend-env 'y 14
                                                  (empty-env))))))

```

## Implementación
Cada ambiente puede ser construido mediante una expresión en la siguiente gramática:

- $<env-exp> ::= (empty-env)$
  $::= (extend-env <identificador> <racket-value> <env-exp>)$

**Representación como estructura de datos:**

- **empty-env:** () $\rightarrow$ Env
  ``` racket
  (define empty-env
	  (lambda () (list 'empty-env)))
	```
- **extend-env:** Var, RacketValue, Env $\rightarrow$ Env
  ```racket
  (define extend-env
	  (lambda (var val env)
		(list 'extend-env var val env)))
	```
- **apply-env:** Env, Var $\rightarrow$ RacketValue
  ```racket
  (define apply-env
  (lambda (env search-var)
    (cond ((eqv? (car env) 'empty-env)
           (report-no-binding-found search-var))
          ((eqv? (car env) 'extend-env)
           (let ((saved-var (cadr env))
                 (saved-val (caddr env))
                 (saved-env (cadddr env)))
             (if (eqv? search-var saved-var)
               saved-val
               (apply-env saved-env search-var))))
          (else
           (report-invalid-env env)))))
	```

**Representación procedimental:**
La interfaz del tipo de dato ambiente tiene una propiedad importante: tiene exactamente una entidad *observadora* `apply-env`. Esto permite representar un ambiente como un procedimiento que toma una variable y retorna su valor asociado.

Se definen `empty-env` y `extend-env` de tal manera que retornan un procedimiento que al ser aplicado devuelve el valor asociado a la variable del ambiente.

- **empty-env:** () $\rightarrow$ Env
  ``` racket
  (define empty-env
	  (lambda ()
	    (lambda (search-var)
	      (report-no-binding-found search-var))))

	```
- **extend-env:** Var, RacketValue, Env $\rightarrow$ Env
  ```racket
  (define extend-env
	  (lambda (saved-var saved-val saved-env)
	    (lambda (search-var)
	      (if (eqv? search-var saved-var)
	        saved-val
	        (apply-env saved-env search-var)))))

	```
- **apply-env:** Env, Var $\rightarrow$ RacketValue
  ```racket
  (define apply-env
	  (lambda(env search-var)
		  (env search-var)))
	```

# Definición de tipos de datos
Los [[3. Especificación Recursiva de Datos|datos recursivos]] pueden definirse mediante diferentes métodos; un claro ejemplo de esto son las [[3. Especificación Recursiva de Datos#^5ff6af|expresiones lambda]]. Asimismo es posible manipular y operar estos tipos de datos mediante algoritmos recursivos, como lo hace, por ejemplo, el procedimiento [[5. Alcance y Ligadura de una Variable#^b9060b|occurs-free?]]. No obstante, esta definición de `occurs-free?` resulta difícil de leer, y establece cierta dependencia con la implementación de las expresiones del cálculo $\lambda$ en forma de listas. Para mejorar estos aspectos, podemos introducir interfaces, las cuales constan de procedimientos *constructores* y procedimientos *observadores*. Estos últimos, a su vez, pueden ser predicados o extractores.